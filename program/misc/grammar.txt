input = declaration expressionpart argument

declaration = 'declare' (tensordeclaration)*
tensordeclaration = tensorname (digit)*
tensorname = alpha { alpha | digit }
alpha = smallalpha | largealpha
smallalpha = [a-z]
largealpha = [A-Z]

expressionpart = 'expression' expr
expr = term (( '+' | '-' ) term)*
term = factor (( '*(' productindices ')') factor | '/' factor)*
productindices = tensorindices ',' tensorindices '->' tensorindices
tensorindices = (smallalpha)*
factor = {'-'} atom ('^' ('(' expr ')' | atom))*
atom = number | function '(' expr ')' | tensorname | '(' expr ')'
number = ['-'] digit* '.' digit* [( 'e' | 'E' ) ['+' | '-'] (digit)*]
digit = [0-9]
function = 'sin' | 'cos' | 'tan' | 'arcsin' | 'arccos' | 'arctan' | 'tanh' | 'exp' 
         | 'log' | 'sign' | 'relu' | 'abs' | 'det' | 'inv' | 'adj'

argument = 'derivative wrt' tensorname